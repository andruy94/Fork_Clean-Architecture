apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../ci.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "ru.terrakok.gitlabclient"

        minSdkVersion 19
        targetSdkVersion 27

        versionName "1.0"
        versionCode getBuildNumber()

        buildConfigField "String", "VERSION_UID", '"' + getBuildUid() + '"'
        buildConfigField "String", "APP_DESCRIPTION", '"Gitfox is an Android client for Gitlab."'
        buildConfigField "String", "FEEDBACK_URL", '"https://gitlab.com/terrakok/gitlab-client/issues"'
        buildConfigField "String", "APP_HOME_PAGE", '"https://gitlab.com/terrakok/gitlab-client"'

        buildConfigField "String", "WEB_AUTH_USER_AGENT", '"gitfox_user_agent"'
        buildConfigField "String", "ORIGIN_GITLAB_ENDPOINT", '"https://gitlab.com/"'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'ru.terrakok.gitlabclient']
            }
        }
    }

    buildTypes {
        debugPG {
            initWith debug

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.debugPG'
            versionNameSuffix ' debugPG'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    supportLibraryVersion = "27.1.1"
    moxyVersion = "1.4.6"
    toothpickVersion = "1.0.6"
    retrofitVersion = "2.2.0"
    markwonVersion = "1.1.1"
}

dependencies {
    //Support
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    //Log
    implementation "com.jakewharton.timber:timber:4.5.1"
    //MVP Moxy
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    //Cicerone Navigation
    implementation "ru.terrakok.cicerone:cicerone:3.0.0"
    //DI
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothpickVersion"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpickVersion"
    //Gson
    implementation "com.google.code.gson:gson:2.8.2"
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    //RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.0"
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    //Adapter simplify
    implementation "com.hannesdorfmann:adapterdelegates3:3.0.1"
    //Image load and cache
    implementation "com.github.bumptech.glide:glide:4.7.1"
    //Markdown to HTML converter
    implementation "ru.noties:markwon:$markwonVersion"
    implementation "ru.noties:markwon-image-loader:$markwonVersion"
    //Bottom navigation bar
    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'
    //Lottie
    implementation 'com.airbnb.android:lottie:2.5.1'
    //Date
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    //FlexBox Layout
    implementation 'com.google.android:flexbox:1.0.0'

    //JUnit
    testImplementation "junit:junit:4.12"
    //Mockito
    testImplementation "org.mockito:mockito-core:2.8.9"
    //Mockito Kotlin
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}