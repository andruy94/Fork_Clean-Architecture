apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply from: '../ci.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.gitlab.terrakok.gitfox"

        minSdkVersion 19
        targetSdkVersion 28

        versionName "1.2.1"
        versionCode 8

        buildConfigField "String", "VERSION_UID", '"' + getBuildUid() + '"'
        buildConfigField "String", "APP_DESCRIPTION", '"Gitfox is an Android client for Gitlab."'
        buildConfigField "String", "FEEDBACK_URL", '"https://gitlab.com/terrakok/gitlab-client/issues"'
        buildConfigField "String", "APP_HOME_PAGE", '"https://gitlab.com/terrakok/gitlab-client"'

        buildConfigField "String", "WEB_AUTH_USER_AGENT", '"gitfox_user_agent"'
        buildConfigField "String", "ORIGIN_GITLAB_ENDPOINT", '"https://gitlab.com/"'

        //todo: put prod value for release
        buildConfigField "String", "OAUTH_APP_ID", '"808b7f51c6634294afd879edd75d5eaf55f1a75e7fe5bd91ca8b7140a5af639d"'
        buildConfigField "String", "OAUTH_SECRET", '"a9dd39c8d2e781b65814007ca0f8b555d34f79b4d30c9356c38bb7ad9909c6f3"'
        buildConfigField "String", "OAUTH_CALLBACK", '"app://gitlab.client/"'

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'ru.terrakok.gitlabclient']
            }
        }
    }

    signingConfigs {
        //todo put key params for release
        prod {
            storeFile file("../keys/play/key.jks")
            storePassword "pass"
            keyAlias "alias"
            keyPassword "pass"
        }
    }

    buildTypes {
        debugPG {
            initWith debug

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            versionNameSuffix ' debugPG'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.prod
        }
    }
}

ext {
    supportLibraryVersion = "28.0.0"
    moxyVersion = "1.4.6"
    toothpickVersion = "1.0.6"
    retrofitVersion = "2.2.0"
    markwonVersion = "1.1.1"
    glideVersion = "4.8.0"
}

dependencies {
    //Support
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    //Log
    implementation "com.jakewharton.timber:timber:4.7.0"
    //MVP Moxy
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
    //Cicerone Navigation
    implementation "ru.terrakok.cicerone:cicerone:4.0.2"
    //DI
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothpickVersion"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpickVersion"
    //Gson
    implementation "com.google.code.gson:gson:2.8.2"
    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    //RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    //Adapter simplify
    implementation "com.hannesdorfmann:adapterdelegates3:3.0.1"
    //Image load and cache
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation ('com.github.bumptech.glide:okhttp3-integration:4.6.1') {
        exclude group: 'glide-parent'
    }
    //Markdown to HTML converter
    implementation "ru.noties:markwon:$markwonVersion"
    implementation "ru.noties:markwon-image-loader:$markwonVersion"
    //Bottom navigation bar
    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'
    //Lottie
    implementation 'com.airbnb.android:lottie:2.5.1'
    //Date
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    //FlexBox Layout
    implementation 'com.google.android:flexbox:1.0.0'
    //Firebase
    implementation 'com.google.firebase:firebase-core:16.0.4'
    //Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    //JUnit
    testImplementation "junit:junit:4.12"
    //Mockito
    testImplementation "org.mockito:mockito-core:2.8.9"
    //Mockito Kotlin
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

apply plugin: 'com.google.gms.google-services'