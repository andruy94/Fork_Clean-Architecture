apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../ci.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "ru.terrakok.gitlabclient"
        minSdkVersion 19
        targetSdkVersion 27

        versionName "1.0"
        versionCode getBuildNumber()
        buildConfigField "String", "VERSION_UID", '"' + getBuildUid() + '"'
        buildConfigField "String", "APP_DESCRIPTION", '"Gitfox is an Android client for Gitlab."'
        buildConfigField "String", "FEEDBACK_URL", '"https://gitlab.com/terrakok/gitlab-client/issues"'
        buildConfigField "String", "APP_HOME_PAGE", '"https://gitlab.com/terrakok/gitlab-client"'

        buildConfigField "String", "WEB_AUTH_USER_AGENT", '"gitfox_user_agent"'
        buildConfigField "String", "ORIGIN_GITLAB_ENDPOINT", '"https://gitlab.com/"'

        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'ru.terrakok.gitlabclient']
            }
        }
    }

    buildTypes {
        debugPG {
            initWith debug

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.debugPG'
            versionNameSuffix ' debugPG'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kapt {
    generateStubs true
}

ext {
    supportLibraryVersion = "27.0.2"
    moxyVersion = "1.4.6"
    toothpickVersion = "1.0.6"
    retrofitVersion = "2.2.0"
}

dependencies {
    //Support
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:1.0.2"
    //Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    //Log
    compile "com.jakewharton.timber:timber:4.5.1"
    //MVP Moxy
    kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
    compile "com.arello-mobile:moxy-app-compat:$moxyVersion"
    //Cicerone Navigation
    compile "ru.terrakok.cicerone:cicerone:3.0.0"
    //DI
    compile "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothpickVersion"
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpickVersion"
    //Gson
    compile "com.google.code.gson:gson:2.8.0"
    //Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:3.6.0"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    //RxJava
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
    compile "io.reactivex.rxjava2:rxjava:2.1.0"
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    //Adapter simplify
    compile "com.hannesdorfmann:adapterdelegates3:3.0.1"
    //Image load and cache
    compile "com.github.bumptech.glide:glide:3.8.0"
    //Markdown to HTML converter
    compile 'ru.noties:markwon:1.0.3'
    //Bottom navigation bar
    compile 'com.aurelhubert:ahbottomnavigation:2.1.0'
    //Custom fonts
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    //Lottie
    compile 'com.airbnb.android:lottie:2.1.0'
    //Date
    compile 'joda-time:joda-time:2.9.9'
    //FlexBox Layout
    compile 'com.google.android:flexbox:0.3.1'

    //JUnit
    testCompile "junit:junit:4.12"
    //Mockito
    testCompile "org.mockito:mockito-core:2.8.9"
    //Mockito Kotlin
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}